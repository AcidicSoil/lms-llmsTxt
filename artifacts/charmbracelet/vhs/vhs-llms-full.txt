# llms-full (private-aware)
> Built from GitHub files and website pages. Large files may be truncated.

--- examples/README.md ---
# Examples

### Gum

Example of recording a demo of [Gum](https://github.com/charmbracelet/gum)
with VHS.

#### Gum File

<img alt="gum file demo with VHS" src="./gum/file.gif" width="600" />

```
Output file.gif

Type "gum file ./src"
Sleep 1s
Enter
Sleep 2s
Down@500ms 4
Up@500ms 1
Sleep 1s
Enter
Sleep 1s
Down@500ms 4
Sleep 1s
Up@500ms 2
Sleep 2s
```

#### Gum Pager

<img alt="gum pager demo with VHS" src="./gum/pager.gif" width="600" />

```
Output pager.gif

Set Padding 20
Set FontSize 16
Set Height 600

Type "gum pager < ~/src/gum/README.md --border normal"
Sleep 1s
Enter
Sleep 2s
Down@25ms 40
Sleep 1s
Up@25ms 30
Sleep 1s
Down@25ms 20
Sleep 3s
```

#### Gum Table

<img alt="gum table demo with VHS" src="./gum/table.gif" width="600" />

```
Output table.gif

Type "gum table < superhero.csv -w 2,12,5,6,6,8,4,20 --height 10"
Enter
Sleep 1s
Down@200ms 10
Sleep 1s
Down@200ms 10
Sleep 1s
Up@200ms 10
Sleep 1s
Enter
Sleep 3s
```

### GitHub CLI

Examples recorded with VHS for the GitHub CLI (`gh`):

#### Issues

<img alt="Simple gh issue demo" src="./gh-cli/gh-issue.gif" width="600" />


```
Output gh-issue.gif

Type "gh issue list"
Sleep 500ms
Enter 1
Sleep 4s

Ctrl+L
Sleep 500ms

Type "gh issue view 19"
Sleep 500ms
Enter

Sleep 5s
```

#### Pull Requests

<img alt="Simple gh pr demo" src="./gh-cli/gh-pr.gif" width="600" />

```
Output gh-pr.gif

Type "gh pr list --state all"
Sleep 500ms
Enter

Sleep 5s
```

### Bubble Tea

Examples recorded with VHS for Bubble Tea.

* [GIFS Renders](https://github.com/charmbracelet/bubbletea/tree/master/examples)
* [Tape Files](./bubbletea)

### jqp

Example of recording a demo of [`jqp`](https://github.com/noahgorstein/jqp)
with VHS.

<img alt="Simple jqp demo with VHS" src="./jqp/jqp.gif" width="600" />

### Glow

Example of recording a demo of [Glow](https://github.com/charmbracelet/glow)
with VHS.

#### Glow Simple

<img alt="Simple glow demo with VHS" src="./glow/glow-simple.gif" width="600" />

```
Output glow-simple.ascii
Output glow-simple.gif

Set Width 1000
Set Height 1000

Type "glow"
Enter
Sleep 1s
Enter
Sleep 1s
Escape
Sleep 1s
Type "q"
Sleep 1s
```

#### Glow

<img alt="Glow demo with VHS" src="./glow/vhs-glow.gif" />

```
Output vhs-glow.gif
Output glow.ascii

Set Width 1600
Set Height 1040

Sleep 1s

Type "glow"

Sleep 100ms

Enter

Sleep 1s

Hide
Tab
Type "/artichoke"
Enter
Down 2
Show

Sleep 0.5s

Down 20

Hide
Escape
Type "l"
Down 5
Show

Sleep 1s
Up@400ms 5

Hide
Type "/ulysses"
Enter
Show

Sleep 0.5s

Down@200ms 20

Hide
Escape
Type "/"
Show

Sleep 0.5s

Type@500ms "todo"
Sleep 1

Hide
Escape
Type "/ulysses"
Enter
Show

Sleep 0.5s

Type@750ms "????"

Hide
Escape
Type "/artichoke"
Enter
Type "m"
Ctrl+A
Right 4
Show

Sleep 1s
Type@250ms "Tasty "
Sleep 1s

Hide
Escape
Down 5
Type "m"
Ctrl+U
Show

Sleep 1s
Type@150ms "Your new internet thing"
Sleep 3s

Hide
Ctrl+C
Show
```


## Links discovered
- [Gum](https://github.com/charmbracelet/gum)
- [GIFS Renders](https://github.com/charmbracelet/bubbletea/tree/master/examples)
- [Tape Files](https://github.com/charmbracelet/vhs/blob/main/examples/bubbletea.md)
- [`jqp`](https://github.com/noahgorstein/jqp)
- [Glow](https://github.com/charmbracelet/glow)

--- examples/cli-ui/README.md ---
# CLI UI

### Format

<img width="600" src="./format.gif" />

```
Output examples/cli-ui/format.gif

Set FontSize 32

Set Width 2200
Set Height 400

Hide
Type "irb --noautocomplete"
Enter
Type "require 'cli/ui'"
Enter
Type "CLI::UI::StdoutRouter.enable"
Enter
Ctrl+L
Show

Type 'puts CLI::UI.fmt "{{red:Red}} {{green:Green}}"'
Sleep .5
Enter
Sleep 5
```

### Nested Frames

<img width="600" src="./nested-frames.gif" />

```
Output examples/cli-ui/nested-frames.gif

Set FontSize 32

Set Width 2000
Set Height 750

Hide
Type "irb --noautocomplete"
Enter
Type "require 'cli/ui'"
Enter
Type "CLI::UI::StdoutRouter.enable"
Enter
Ctrl+L
Show

Type "CLI::UI::Frame.open('Frame 1') do"
Enter
Type "  CLI::UI::Frame.open('Frame 2') { puts 'inside frame 2' }"
Enter
Type "  puts 'inside frame 1'"
Enter
Type "end"

Sleep 1

Enter

Sleep 3
```

### Progress
<img width="600" src="./progress.gif" />

```
Output examples/cli-ui/progress.gif

Set FontSize 32

Set Width 2200
Set Height 400

Hide
Type "irb --noautocomplete"
Enter
Type "require 'cli/ui'"
Enter
Type "CLI::UI::StdoutRouter.enable"
Enter
Ctrl+L
Show

Type "CLI::UI::Progress.progress { |bar| 100.times { sleep 0.02; bar.tick } }"
Sleep .5
Enter
Sleep 5
```

### Spinner
<img width="600" src="./spinner.gif" />

```
Output examples/cli-ui/spinner.gif

Set FontSize 32

Set Width 2200
Set Height 400

Hide
Type "irb --noautocomplete"
Enter
Type "require 'cli/ui'"
Enter
Type "CLI::UI::StdoutRouter.enable"
Enter
Ctrl+L
Show

Type "CLI::UI::Spinner.spin('Spinning...') { sleep 3 }"
Sleep .5
Enter
Sleep 5
```

### Status Widget
<img width="600" src="./status-widget.gif" />

```
Output examples/cli-ui/status-widget.gif

Set FontSize 32

Set Width 2200
Set Height 400

Hide
Type "irb --noautocomplete"
Enter
Type "require 'cli/ui'"
Enter
Type "CLI::UI::StdoutRouter.enable"
Enter
Ctrl+L
Show

Type 'CLI::UI::Spinner.spin("Building: {{@widget/status:1:2:3:4}}") { |spinner| sleep 3 }'
Sleep .5
Enter
Sleep 5
```

### Symbols
<img width="600" src="./symbols.gif" />

```
Output examples/cli-ui/symbols.gif

Set FontSize 32

Set Width 2200
Set Height 400

Hide
Type "irb --noautocomplete"
Enter
Type "require 'cli/ui'"
Enter
Type "CLI::UI::StdoutRouter.enable"
Enter
Ctrl+L
Show

Type 'puts CLI::UI.fmt "{{*}} {{v}} {{?}} {{x}}"'
Sleep .5
Enter
Sleep 5
```

### Text Prompt
<img width="600" src="./text-prompt.gif" />

```
Output examples/cli-ui/text-prompt.ascii
Output examples/cli-ui/text-prompt.gif

Set FontSize 32

Set Width 2200
Set Height 500

Hide
Type "irb --noautocomplete"
Enter
Type "require 'cli/ui'"
Enter
Type "CLI::UI::StdoutRouter.enable"
Enter
Ctrl+L
Show

Type "CLI::UI.ask('Is CLI UI Awesome?', default: 'It is great!')"
Sleep .5
Enter
Sleep 1
Type "I love it!"
Sleep 1
Enter
Sleep 3
```


--- examples/commands/README.md ---
# Commands

### Arrow

<img width="600" src="./arrow.gif" />

```
Output examples/commands/arrow.gif
Set FontSize 42
Set Height 225

Type "Navigate around"
Sleep .25
Left 10
Sleep 1
Right@50ms 10
Sleep 1
```

### Backspace

<img width="600" src="./backspace.gif" />

```
Output examples/commands/backspace.gif
Set FontSize 42
Set Height 225

Type@50ms "Delete anything..."
Backspace 18
Sleep 1
```

### Comment

<img width="600" src="./comment.gif" />

```
Output examples/commands/comment.gif
Set Height 500
Set Width 1000

# This is a comment.
# These are ignored by the parser so you can write whatever you want!

# Quickly comment out a command you don't need.
# Type "Hello, world!"

Sleep 1
```

### Ctrl

<img width="600" src="./ctrl.gif" />

```
Output examples/commands/ctrl.gif
Set FontSize 42
Set Height 225

Sleep 1
Ctrl+R
Sleep 1
```

### Enter

<img width="600" src="./enter.gif" />

```
Output examples/commands/enter.gif
Set FontSize 42
Set Height 350

Sleep 1
Enter@.5 2
Sleep 1
```

### Hide

<img width="600" src="./hide.gif" />

```
Output examples/commands/hide.gif

Set FontSize 42
Set Height 300

Hide
Type "You won't see this being typed." Ctrl+C
Show
Type "You will see this being typed."

Sleep 2
```

### Show

<img width="600" src="./show.gif" />

```
Output examples/commands/show.gif

Hide
Type "export HIDDEN=wow"
Enter
Ctrl+L
Show

Type "echo $HIDDEN"
Enter
Sleep 1
```

### Space

<img width="600" src="./space.gif" />

```
Output examples/commands/space.gif
Set FontSize 42
Set Height 225

Sleep .25
Space 10
Sleep 1
```

### Tab

<img width="600" src="./tab.gif" />

```
Output examples/commands/tab.gif
Set FontSize 42
Set Height 300

Type "cd ."
Sleep 0.5s
Tab@0.5s 2
Sleep 1s
```

### Type

<img width="600" src="./type.gif" />

```
Output examples/commands/type.gif
Set FontSize 42
Set Height 225

Sleep 1

# Type something
Type "Whatever you want"

Sleep 1 Ctrl+U  Sleep 1

# Type something really slowly!
Type@500ms "Slow down there, partner."

Sleep 1
```



--- examples/gh-cli/README.md ---
# GitHub CLI

### GitHub PR List

<img width="600" src="./gh-pr.gif" />

```
Output examples/gh-cli/gh-pr.gif

Type "gh pr list --state all"
Sleep 500ms
Enter

Sleep 5s
```

### GitHub Issue List

<img width="600" src="./gh-issue.gif" />

```
Output examples/gh-cli/gh-issue.gif

Type "gh issue list"
Sleep 500ms
Enter 1
Sleep 4s

Ctrl+L
Sleep 500ms

Type "gh issue view 19"
Sleep 500ms
Enter

Sleep 5s
```


--- examples/glow/README.md ---
# Glow

### Glow Simple

<img width="600" src="./glow-simple.gif" />

```
Output examples/glow/glow-simple.ascii
Output examples/glow/glow-simple.gif

Set Width 1000
Set Height 1050

Type "glow"
Enter
Sleep 1s
Enter
Sleep 1s
Escape
Sleep 1s
Type "q"
Sleep 1s
```

### Glow Full

<img width="600" src="./vhs-glow.gif" />

```
Output examples/glow/vhs-glow.gif
Output examples/glow/glow.ascii

Set Width 1600
Set Height 1040

Sleep 1s

Type "glow"

Sleep 100ms

Enter

Sleep 1s

Hide
Tab
Type "/artichoke"
Enter
Down 2
Show

Sleep 0.5s

Down 20

Hide
Escape
Type "l"
Down 5
Show

Sleep 1s
Up@400ms 5

Hide
Type "/ulysses"
Enter
Show

Sleep 0.5s

Down@200ms 20

Hide
Escape
Type "/"
Show

Sleep 0.5s

Type@500ms "todo"
Sleep 1

Hide
Escape
Type "/ulysses"
Enter
Show

Sleep 0.5s

Type@750ms "????"

Hide
Escape
Type "/artichoke"
Enter
Type "m"
Ctrl+A
Right 4
Show

Sleep 1s
Type@250ms "Tasty "
Sleep 1s

Hide
Escape
Down 5
Type "m"
Ctrl+U
Show

Sleep 1s
Type@150ms "Your new internet thing"
Sleep 3s

Hide
Ctrl+C
Show  
```


--- examples/gum/README.md ---
# Gum

### File

<img width="600" src="file.gif" />

```
Output examples/gum/file.gif

Type "gum file ./src"
Sleep 0.5s
Enter
Sleep 0.5s
Down@250ms 4
Up@250ms 1
Sleep 0.5s
Enter
Sleep 0.5s
Down@250ms 4
Sleep 0.5s
Up@250ms 2
Sleep 1s
```

### Pager

<img width="600" src="pager.gif" />

```
Output examples/gum/pager.gif

Set Padding 32
Set FontSize 16
Set Height 600

Type "gum pager < ~/src/gum/README.md --border normal"
Sleep 0.5s
Enter
Sleep 1s
Down@15ms 40
Sleep 0.5s
Up@15ms 30
Sleep 0.5s
Down@15ms 20
Sleep 2s
```

### Table

<img width="600" src="pager.gif" />

```
Output examples/gum/table.gif

Type "gum table < superhero.csv -w 2,12,5,6,6,8,4,20 --height 10"
Enter
Sleep 0.5s
Down 10
Sleep 0.5s
Down 10
Sleep 0.5s
Up 10
Sleep 0.5s
Enter
Sleep 2s
```


--- examples/jqp/README.md ---
# JQP

<img width="600" src="./jqp.gif" />

```
Output examples/jqp/jqp.gif
Set Width 2400
Set Height 1600

Hide
Type "curl https://dummyjson.com/products | jqp"
Enter
Sleep 1
Show

Sleep 1

Type@.2'[ .products[] | select(.category=="smartphones") ]'
Sleep 1
Enter

Sleep 1
Tab

Sleep 1
Down@25ms 20

Sleep .5
Down@25ms 20

Tab

Sleep .5
Down@25ms 30

Sleep 1
Ctrl+S

Sleep 3

Escape

Sleep 3
```


--- examples/neofetch/README.md ---
# Neofetch

<img width="600" src="neofetch.gif" />

```
# Source code for the VHS neofetch example.
#
# To run:
#
#     vhs < neofetch.tape

Output examples/neofetch/neofetch.gif
Output examples/neofetch/neofetch.mp4
Output examples/neofetch/neofetch.webm
Output examples/neofetch/frames/

Set TypingSpeed 75ms
Set FontSize 22
Set Width 1300
Set Height 650

Type "neofetch"

Sleep 500ms

Enter

Sleep 2s

Type "Welcome to VHS!"

Sleep 1

Space

Type "A tool for generating terminal GIFs from code."

Sleep 5s
```


--- examples/settings/README.md ---
# Settings

### Width

<img width="600" src="./width.gif" />


```
Output examples/settings/width.gif

Set Height 200
Set Width 475

Type "I'm a little teapot"

Sleep 1s
```

### Height

<img width="600" src="./height.gif" />

```
Output examples/settings/height.gif

Set Height 1000
Set Width 1000
Set FontSize 50

Type "I'm a big teapot"

Sleep 1s
```

### Font Family

<img width="600" src="./set-font-family.gif" />

```
Output examples/settings/set-font-family.gif

Set FontSize 42
Set Height 225
Set FontFamily "Monoflow"

Type "One moment please..."

Sleep 0.5s
```

### Font Size

<img width="600" src="./set-font-size-10.gif" />
<img width="600" src="./set-font-size-20.gif" />
<img width="600" src="./set-font-size-40.gif" />

```
Output examples/settings/set-font-size-10.gif

Set Height 225

Set FontSize 10

Type "Super Mushroom!!!"

Sleep 1
```

### Letter Spacing

<img width="600" src="./set-letter-spacing.gif" />

```
Output examples/settings/set-letter-spacing.gif
Set FontSize 42
Set Height 225
Set LetterSpacing 20

Type "Smooooooooooooth"

Sleep 2
```

### Line Height

<img width="600" src="./set-line-height.gif" />

```
Output examples/settings/set-line-height.gif
Set Height 400
Set FontSize 32
Set LineHeight 1.8

Type "Stay far away!"
Sleep .5
Enter
Sleep 1
```

### Padding

<img width="600" src="./set-padding.gif" />

```
Output examples/settings/set-padding.gif
Set FontSize 32
Set Height 150
Set Padding 0

Type "I'm so edgy!"
Sleep 1
```

### Margins

<img width="600" src="./set-margin.gif">

```
Output "examples/settings/set-margin.gif"
Set FontSize 20
Set Width 600
Set Height 300
Set Padding 20

# You can also use an image:
# Set MarginFill "path/to/image.png"
Set MarginFill "#79B8C3"
Set Margin 20

Type "I'm on top of an image!"
Sleep 2s
```


### Window Bars

<img width="600" src="./set-bar.gif">

```
Output "examples/settings/set-bar.gif"
Set FontSize 25
Set Width 600
Set Height 300
Set Padding 20

Set WindowBar Colorful
Set WindowBarSize 40

Type "I have a bar!"
Sleep 2s
```


### Border Radius

<img width="600" src="./set-border-radius.gif">

```
Output "examples/settings/set-border-radius.gif"
Set FontSize 25
Set Width 600
Set Height 300
Set Padding 20

Set MarginFill "#79B8C3"
Set Margin 20

Set BorderRadius 20

Type "I have round corners."
Sleep 2s
```


### Theme

<img width="600" src="./set-theme.gif" />

```
Output examples/settings/set-theme.gif
Set FontSize 42
Set Height 225
Set Theme { "name": "Whimsy", "black": "#535178", "red": "#ef6487", "green": "#5eca89", "yellow": "#fdd877", "blue": "#65aef7", "purple": "#aa7ff0", "cyan": "#43c1be", "white": "#ffffff", "brightBlack": "#535178", "brightRed": "#ef6487", "brightGreen": "#5eca89", "brightYellow": "#fdd877", "brightBlue": "#65aef7", "brightPurple": "#aa7ff0", "brightCyan": "#43c1be", "brightWhite": "#ffffff", "background": "#29283b", "foreground": "#b3b0d6", "selectionBackground": "#3d3c58", "cursorAccent": "#b3b0d6", "cursor": "#b3b0d6" }

Type "I can't make up my mind."
Sleep 1
```

### Typing Speed

<img width="600" src="./set-typing-speed.gif" />

```
Output examples/settings/set-typing-speed.gif
Set Height 400
Set FontSize 32

Set TypingSpeed 25ms
Type "Bunny"
Ctrl+C
Set TypingSpeed 50ms
Type "Human"
Ctrl+C
Set TypingSpeed 100ms
Type "Tortoise"
Ctrl+C
Type@250ms "Sloth"
Ctrl+C
Sleep 1
```


--- examples/slides/README.md ---
# Slides

<img width="600" src="./slides.gif" />

```
Output examples/slides/slides.gif
Set FontSize 32
Set Padding 0
Set Framerate 1
Set Width 1600
Set Height 1200

Hide
Type "slides"
Enter
Show

Sleep 1s

Type@1s "n"

Sleep 1s

Ctrl+E

Sleep 1s

Type@1s "nnn"

Sleep 1s
```


--- README.md ---
# VHS

<p>
  <img src="https://user-images.githubusercontent.com/42545625/198402537-12ca2f6c-0779-4eb8-a67c-8db9cb3df13c.png#gh-dark-mode-only" width="500" />
  <img src="https://user-images.githubusercontent.com/42545625/198402542-a305f669-a05a-4d91-b18b-ca76e72b655a.png#gh-light-mode-only" width="500" />
  <br>
  <a href="https://github.com/charmbracelet/vhs/releases"><img src="https://img.shields.io/github/release/charmbracelet/vhs.svg" alt="Latest Release"></a>
  <a href="https://pkg.go.dev/github.com/charmbracelet/vhs?tab=doc"><img src="https://godoc.org/github.com/golang/gddo?status.svg" alt="Go Docs"></a>
  <a href="https://github.com/charmbracelet/vhs/actions"><img src="https://github.com/charmbracelet/vhs/workflows/build/badge.svg" alt="Build Status"></a>
</p>

Write terminal GIFs as code for integration testing and demoing your CLI tools.

<img alt="Welcome to VHS" src="https://stuff.charm.sh/vhs/examples/neofetch_3.gif" width="600" />

The above example was generated with VHS ([view source](./examples/neofetch/neofetch.tape)).

## Tutorial

To get started, [install VHS](#installation) and create a new `.tape` file.

```sh
vhs new demo.tape
```

Open the `.tape` file with your favorite `$EDITOR`.

```sh
vim demo.tape
```

Tape files consist of a series of [commands](#vhs-command-reference). The commands are
instructions for VHS to perform on its virtual terminal. For a list of all
possible commands see [the command reference](#vhs-command-reference).

```elixir
# Where should we write the GIF?
Output demo.gif

# Set up a 1200x600 terminal with 46px font.
Set FontSize 46
Set Width 1200
Set Height 600

# Type a command in the terminal.
Type "echo 'Welcome to VHS!'"

# Pause for dramatic effect...
Sleep 500ms

# Run the command by pressing enter.
Enter

# Admire the output for a bit.
Sleep 5s
```

Once you've finished, save the file and feed it into VHS.

```sh
vhs demo.tape
```

All done! You should see a new file called `demo.gif` (or whatever you named
the `Output`) in the directory.

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/demo.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/demo.gif">
  <img width="600" alt="A GIF produced by the VHS code above" src="https://stuff.charm.sh/vhs/examples/demo.gif">
</picture>

For more examples see the [`examples/`](https://github.com/charmbracelet/vhs/tree/main/examples) directory.

## Installation

> [!NOTE]
> VHS requires [`ttyd`](https://github.com/tsl0922/ttyd) and [`ffmpeg`](https://ffmpeg.org) to be installed and available on your `PATH`.

Use a package manager:

```sh
# macOS or Linux
brew install vhs

# Arch Linux (btw)
pacman -S vhs

# Nix
nix-env -iA nixpkgs.vhs

# Windows using scoop
scoop install vhs
```

Or, use Docker to run VHS directly, dependencies included:

```sh
docker run --rm -v $PWD:/vhs ghcr.io/charmbracelet/vhs <cassette>.tape
```

Or, download it:

- [Packages][releases] are available in Debian and RPM formats
- [Binaries][releases] are available for Linux, macOS, and Windows

Or, just install it with `go`:

```sh
go install github.com/charmbracelet/vhs@latest
```

<details>
<summary>Windows, Debian, Ubuntu, Fedora, RHEL, Void Instructions</summary>

- Debian / Ubuntu

```sh
# Debian/Ubuntu
sudo mkdir -p /etc/apt/keyrings
curl -fsSL https://repo.charm.sh/apt/gpg.key | sudo gpg --dearmor -o /etc/apt/keyrings/charm.gpg
echo "deb [signed-by=/etc/apt/keyrings/charm.gpg] https://repo.charm.sh/apt/ * *" | sudo tee /etc/apt/sources.list.d/charm.list
# Install ttyd from https://github.com/tsl0922/ttyd/releases
sudo apt update && sudo apt install vhs ffmpeg
```

- Fedora / RHEL

```sh
echo '[charm]
name=Charm
baseurl=https://repo.charm.sh/yum/
enabled=1
gpgcheck=1
gpgkey=https://repo.charm.sh/yum/gpg.key' | sudo tee /etc/yum.repos.d/charm.repo
# Install ttyd from https://github.com/tsl0922/ttyd/releases
sudo yum install vhs ffmpeg
```

- Void

```sh
sudo xbps-install vhs
```

- Windows

```sh
winget install charmbracelet.vhs
# or scoop
scoop install vhs
```

</details>

[releases]: https://github.com/charmbracelet/vhs/releases

## Record Tapes

VHS has the ability to generate tape files from your terminal actions!

To record to a tape file, run:

```bash
vhs record > cassette.tape
```

Perform any actions you want and then `exit` the terminal session to stop
recording. You may want to manually edit the generated `.tape` file to add
settings or modify actions. Then, you can generate the GIF:

```bash
vhs cassette.tape
```

## Publish Tapes

VHS allows you to publish your GIFs to our servers for easy sharing with your
friends and colleagues. Specify which file you want to share, then use the
`publish` sub-command to host it on `vhs.charm.sh`. The output will provide you
with links to share your GIF via browser, HTML, and Markdown.

```bash
vhs publish demo.gif
```

## The VHS Server

VHS has an SSH server built in! When you self-host VHS you can access it as
though it were installed locally. VHS will have access to commands and
applications on the host, so you don't need to install them on your machine.

To start the server run:

```sh
vhs serve
```

<details>
<summary>Configuration Options</summary>

- `VHS_PORT`: The port to listen on (`1976`)
- `VHS_HOST`: The host to listen on (`localhost`)
- `VHS_GID`: The Group ID to run the server as (current user's GID)
- `VHS_UID`: The User ID to run the server as (current user's UID)
- `VHS_KEY_PATH`: The path to the SSH key to use (`.ssh/vhs_ed25519`)
- `VHS_AUTHORIZED_KEYS_PATH`: The path to the authorized keys file (empty, publicly accessible)

</details>

Then, simply access VHS from a different machine via `ssh`:

```sh
ssh vhs.example.com < demo.tape > demo.gif
```

## VHS Command Reference

> [!NOTE]
> You can view all VHS documentation on the command line with `vhs manual`.

There are a few basic types of VHS commands:

- [`Output <path>`](#output): specify file output
- [`Require <program>`](#require): specify required programs for tape file
- [`Set <Setting> Value`](#settings): set recording settings
- [`Type "<characters>"`](#type): emulate typing
- [`Left`](#arrow-keys) [`Right`](#arrow-keys) [`Up`](#arrow-keys) [`Down`](#arrow-keys): arrow keys
- [`Backspace`](#backspace) [`Enter`](#enter) [`Tab`](#tab) [`Space`](#space): special keys
- [`Ctrl[+Alt][+Shift]+<char>`](#ctrl): press control + key and/or modifier
- [`Sleep <time>`](#sleep): wait for a certain amount of time
- [`Wait[+Screen][+Line] /regex/`](#wait): wait for specific conditions
- [`Hide`](#hide): hide commands from output
- [`Show`](#show): stop hiding commands from output
- [`Screenshot`](#screenshot): screenshot the current frame
- [`Copy/Paste`](#copy--paste): copy and paste text from clipboard.
- [`Source`](#source): source commands from another tape
- [`Env <Key> Value`](#env): set environment variables

### Output

The `Output` command allows you to specify the location and file format
of the render. You can specify more than one output in a tape file which
will render them to the respective locations.

```elixir
Output out.gif
Output out.mp4
Output out.webm
Output frames/ # a directory of frames as a PNG sequence
```

### Require

The `Require` command allows you to specify dependencies for your tape file.
These are useful to fail early if a required program is missing from the
`$PATH`, and it is certain that the VHS execution will not work as expected.

Require commands must be defined at the top of a tape file, before any non-
setting or non-output command.

```elixir
# A tape file that requires gum and glow to be in the $PATH
Require gum
Require glow
```

### Settings

The `Set` command allows you to change global aspects of the terminal, such as
the font settings, window dimensions, and GIF output location.

Setting must be administered at the top of the tape file. Any setting (except
`TypingSpeed`) applied after a non-setting or non-output command will be
ignored.

#### Set Shell

Set the shell with the `Set Shell <shell>` command

```elixir
Set Shell fish
```

#### Set Font Size

Set the font size with the `Set FontSize <number>` command.

```elixir
Set FontSize 10
Set FontSize 20
Set FontSize 40
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/font-size-10.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/font-size-10.gif">
  <img width="600" alt="Example of setting the font size to 10 pixels" src="https://stuff.charm.sh/vhs/examples/font-size-10.gif">
</picture>

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/font-size-20.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/font-size-20.gif">
  <img width="600" alt="Example of setting the font size to 20 pixels" src="https://stuff.charm.sh/vhs/examples/font-size-20.gif">
</picture>

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/font-size-40.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/font-size-40.gif">
  <img width="600" alt="Example of setting the font size to 40 pixels" src="https://stuff.charm.sh/vhs/examples/font-size-40.gif">
</picture>

#### Set Font Family

Set the font family with the `Set FontFamily "<font>"` command

```elixir
Set FontFamily "Monoflow"
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/font-family.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/font-family.gif">
  <img width="600" alt="Example of changing the font family to Monoflow" src="https://stuff.charm.sh/vhs/examples/font-family.gif">
</picture>

#### Set Width

Set the width of the terminal with the `Set Width` command.

```elixir
Set Width 300
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/width.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/width.gif">
  <img width="300" alt="Example of changing the width of the terminal" src="https://stuff.charm.sh/vhs/examples/width.gif">
</picture>

#### Set Height

Set the height of the terminal with the `Set Height` command.

```elixir
Set Height 1000
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/height.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/height.gif">
  <img width="300" alt="Example of changing the height of the terminal" src="https://stuff.charm.sh/vhs/examples/height.gif">
</picture>

#### Set Letter Spacing

Set the spacing between letters (tracking) with the `Set LetterSpacing`
Command.

```elixir
Set LetterSpacing 20
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/letter-spacing.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/letter-spacing.gif">
  <img width="600" alt="Example of changing the letter spacing to 20 pixels between characters" src="https://stuff.charm.sh/vhs/examples/letter-spacing.gif">
</picture>

#### Set Line Height

Set the spacing between lines with the `Set LineHeight` Command.

```elixir
Set LineHeight 1.8
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/line-height.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/line-height.gif">
  <img width="600" alt="Example of changing the line height to 1.8" src="https://stuff.charm.sh/vhs/examples/line-height.gif">
</picture>

#### Set Typing Speed

```elixir
Set TypingSpeed 500ms # 500ms
Set TypingSpeed 1s    # 1s
```

Set the typing speed of seconds per key press. For example, a typing speed of
`0.1` would result in a `0.1s` (`100ms`) delay between each character being typed.

This setting can also be overwritten per command with the `@<time>` syntax.

```elixir
Set TypingSpeed 0.1
Type "100ms delay per character"
Type@500ms "500ms delay per character"
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/typing-speed.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/typing-speed.gif">
  <img width="600" alt="Example of using the Type command in VHS" src="https://stuff.charm.sh/vhs/examples/typing-speed.gif">
</picture>

#### Set Theme

Set the theme of the terminal with the `Set Theme` command. The theme value
should be a JSON string with the base 16 colors and foreground + background.

```elixir
Set Theme { "name": "Whimsy", "black": "#535178", "red": "#ef6487", "green": "#5eca89", "yellow": "#fdd877", "blue": "#65aef7", "magenta": "#aa7ff0", "cyan": "#43c1be", "white": "#ffffff", "brightBlack": "#535178", "brightRed": "#ef6487", "brightGreen": "#5eca89", "brightYellow": "#fdd877", "brightBlue": "#65aef7", "brightMagenta": "#aa7ff0", "brightCyan": "#43c1be", "brightWhite": "#ffffff", "background": "#29283b", "foreground": "#b3b0d6", "selection": "#3d3c58", "cursor": "#b3b0d6" }
```

<img alt="Example of changing the theme to Whimsy" src="https://stuff.charm.sh/vhs/examples/theme.gif" width="600" />

You can also set themes by name:

```elixir
Set Theme "Catppuccin Frappe"
```

See the full list by running `vhs themes`, or in [THEMES.md](./THEMES.md).

#### Set Padding

Set the padding (in pixels) of the terminal frame with the `Set Padding`
command.

```elixir
Set Padding 0
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/padding.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/padding.gif">
  <img width="600" alt="Example of setting the padding" src="https://stuff.charm.sh/vhs/examples/padding.gif">
</picture>

#### Set Margin

Set the margin (in pixels) of the video with the `Set Margin` command.

```elixir
Set Margin 60
Set MarginFill "#6B50FF"
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://vhs.charm.sh/vhs-1miKMtNHenh7O4sv76TMwG.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://vhs.charm.sh/vhs-1miKMtNHenh7O4sv76TMwG.gif">
  <img width="600" alt="Example of setting the margin" src="https://vhs.charm.sh/vhs-1miKMtNHenh7O4sv76TMwG.gif">
</picture>

#### Set Window Bar

Set the type of window bar (Colorful, ColorfulRight, Rings, RingsRight) on the terminal window with the `Set WindowBar` command.

```elixir
Set WindowBar Colorful
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://vhs.charm.sh/vhs-4VgviCu38DbaGtbRzhtOUI.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://vhs.charm.sh/vhs-4VgviCu38DbaGtbRzhtOUI.gif">
  <img width="600" alt="Example of setting the margin" src="https://vhs.charm.sh/vhs-4VgviCu38DbaGtbRzhtOUI.gif">
</picture>

#### Set Border Radius

Set the border radius (in pixels) of the terminal window with the `Set BorderRadius` command.

```elixir
# You'll likely want to add a Margin + MarginFill if you use BorderRadius.
Set Margin 20
Set MarginFill "#674EFF"
Set BorderRadius 10
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://vhs.charm.sh/vhs-4nYoy6IsUKmleJANG7N1BH.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://vhs.charm.sh/vhs-4nYoy6IsUKmleJANG7N1BH.gif">
  <img width="400" alt="Example of setting the margin" src="https://vhs.charm.sh/vhs-4nYoy6IsUKmleJANG7N1BH.gif">
</picture>

#### Set Framerate

Set the rate at which VHS captures frames with the `Set Framerate` command.

```elixir
Set Framerate 60
```

#### Set Playback Speed

Set the playback speed of the final render.

```elixir
Set PlaybackSpeed 0.5 # Make output 2 times slower
Set PlaybackSpeed 1.0 # Keep output at normal speed (default)
Set PlaybackSpeed 2.0 # Make output 2 times faster
```

#### Set Loop Offset

Set the offset for when the GIF loop should begin. This allows you to make the
first frame of the GIF (generally used for previews) more interesting.

```elixir
Set LoopOffset 5 # Start the GIF at the 5th frame
Set LoopOffset 50% # Start the GIF halfway through
```

#### Set Cursor Blink

Set whether the cursor should blink. Enabled by default.

```elixir
Set CursorBlink false
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://vhs.charm.sh/vhs-3rMCb80VEkaDdTOJMCrxKy.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://vhs.charm.sh/vhs-3rMCb80VEkaDdTOJMCrxKy.gif">
  <img width="600" alt="Example of setting the cursor blink." src="https://vhs.charm.sh/vhs-3rMCb80VEkaDdTOJMCrxKy.gif">
</picture>

### Type

Use `Type` to emulate key presses. That is, you can use `Type` to script typing
in a terminal. Type is handy for both entering commands and interacting with
prompts and TUIs in the terminal. The command takes a string argument of the
characters to type.

You can set the standard typing speed with [`Set TypingSpeed`](#set-typing-speed)
and override it in places with a `@time` argument.

```elixir
# Type something
Type "Whatever you want"

# Type something really slowly!
Type@500ms "Slow down there, partner."
```

Escape single and double quotes with backticks.

```elixir
Type `VAR="Escaped"`
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/type.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/type.gif">
  <img width="600" alt="Example of using the Type command in VHS" src="https://stuff.charm.sh/vhs/examples/type.gif">
</picture>

### Keys

Key commands take an optional `@time` and optional repeat `count` for repeating
the key press every interval of `<time>`.

```
Key[@<time>] [count]
```

#### Backspace

Press the backspace key with the `Backspace` command.

```elixir
Backspace 18
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/backspace.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/backspace.gif">
  <img width="600" alt="Example of pressing the Backspace key 18 times" src="https://stuff.charm.sh/vhs/examples/backspace.gif">
</picture>

#### Ctrl

You can access the control modifier and send control sequences with the `Ctrl`
command.

```elixir
Ctrl+R
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/ctrl.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/ctrl.gif">
  <img width="600" alt="Example of pressing the Ctrl+R key to reverse search" src="https://stuff.charm.sh/vhs/examples/ctrl.gif">
</picture>

#### Enter

Press the enter key with the `Enter` command.

```elixir
Enter 2
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/enter.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/enter.gif">
  <img width="600" alt="Example of pressing the Enter key twice" src="https://stuff.charm.sh/vhs/examples/enter.gif">
</picture>

#### Arrow Keys

Press any of the arrow keys with the `Up`, `Down`, `Left`, `Right` commands.

```elixir
Up 2
Down 2
Left
Right
Left
Right
Type "B"
Type "A"
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/arrow.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/arrow.gif">
  <img width="600" alt="Example of pressing the arrow keys to navigate text" src="https://stuff.charm.sh/vhs/examples/arrow.gif">
</picture>

#### Tab

Enter a tab with the `Tab` command.

```elixir
Tab@500ms 2
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/tab.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/tab.gif">
  <img width="600" alt="Example of pressing the tab key twice for autocomplete" src="https://stuff.charm.sh/vhs/examples/tab.gif">
</picture>

#### Space

Press the space bar with the `Space` command.

```elixir
Space 10
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/space.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/space.gif">
  <img width="600" alt="Example of pressing the space key" src="https://stuff.charm.sh/vhs/examples/space.gif">
</picture>

#### Page Up / Down

Press the Page Up / Down keys with the `PageUp` or `PageDown` commands.

```elixir
PageUp 3
PageDown 5
```

### Wait

The `Wait` command allows you to wait for something to appear on the screen.
This is useful when you need to wait on something to complete, even if you don't
know how long it'll take, while including it in the recording like a spinner or
loading state.
The command takes a regular expression as an argument, and optionally allows to
set the duration to wait and if you want to check the whole screen or just the
last line (the scope).

```elixir
Wait
Wait            /World/
Wait+Screen     /World/
Wait+Line       /World/
Wait@10ms       /World/
Wait+Line@10ms  /World/
```

The default regular expression is `/>$/`, the wait timeout is `15s`, and the
default scope is `Line`.

### Sleep

The `Sleep` command allows you to continue capturing frames without interacting
with the terminal. This is useful when you need to wait on something to
complete while including it in the recording like a spinner or loading state.
The command takes a number argument in seconds.

```elixir
Sleep 0.5   # 500ms
Sleep 2     # 2s
Sleep 100ms # 100ms
Sleep 1s    # 1s
```

### Hide

The `Hide` command instructs VHS to stop capturing frames. It's useful to pause
a recording to perform hidden commands.

```elixir
Hide
```

This command is helpful for performing any setup and cleanup required to record
a GIF, such as building the latest version of a binary and removing the binary
once the demo is recorded.

```elixir
Output example.gif

# Setup
Hide
Type "go build -o example . && clear"
Enter
Show

# Recording...
Type 'Running ./example'
...
Enter

# Cleanup
Hide
Type 'rm example'
Enter
```

### Show

The `Show` command instructs VHS to begin capturing frames, again. It's useful
after a `Hide` command to resume frame recording for the output.

```elixir
Hide
Type "You won't see this being typed."
Show
Type "You will see this being typed."
```

<picture>
  <source media="(prefers-color-scheme: dark)" srcset="https://stuff.charm.sh/vhs/examples/hide.gif">
  <source media="(prefers-color-scheme: light)" srcset="https://stuff.charm.sh/vhs/examples/hide.gif">
  <img width="600" alt="Example of typing something while hidden" src="https://stuff.charm.sh/vhs/examples/hide.gif">
</picture>

### Screenshot

The `Screenshot` command captures the current frame (png format).

```elixir
# At any point...
Screenshot examples/screenshot.png
```

### Copy / Paste

The `Copy` and `Paste` copy and paste the string from clipboard.

```elixir
Copy "https://github.com/charmbracelet"
Type "open "
Sleep 500ms
Paste
```

### Env

`Env` command sets the environment variable via key-value pair.

```elixir
Env HELLO "WORLD"

Type "echo $HELLO"
Enter
Sleep 1s
```

### Source

The `source` command allows you to execute commands from another tape.

```elixir
Source config.tape
```

---

## Continuous Integration

You can hook up VHS to your CI pipeline to keep your GIFs up-to-date with
the official VHS GitHub Action:

> [‚öôÔ∏è charmbracelet/vhs-action](https://github.com/charmbracelet/vhs-action)

VHS can also be used for integration testing. Use the `.txt` or `.ascii` output
to generate golden files. Store these files in a git repository to ensure there
are no diffs between runs of the tape file.

```elixir
Output golden.ascii
```

## Syntax Highlighting

There‚Äôs a tree-sitter grammar for `.tape` files available for editors that
support syntax highlighting with tree-sitter:

> [üå≥ charmbracelet/tree-sitter-vhs](https://github.com/charmbracelet/tree-sitter-vhs)

It works great with Neovim, Emacs, and so on!

## Contributing

See [contributing][contribute].

[contribute]: https://github.com/charmbracelet/vhs/contribute

## Feedback

We‚Äôd love to hear your thoughts on this project. Feel free to drop us a note!

- [Twitter](https://twitter.com/charmcli)
- [The Fediverse](https://mastodon.social/@charmcli)
- [Discord](https://charm.sh/chat)

## License

[MIT](https://github.com/charmbracelet/vhs/raw/main/LICENSE)

---

Part of [Charm](https://charm.sh).

<a href="https://charm.sh/">
  <img
    alt="The Charm logo"
    width="400"
    src="https://stuff.charm.sh/charm-badge.jpg"
  />
</a>

CharmÁÉ≠Áà±ÂºÄÊ∫ê ‚Ä¢ Charm loves open source


## Links discovered
- [view source](https://github.com/charmbracelet/vhs/blob/main/examples/neofetch/neofetch.tape)
- [`examples/`](https://github.com/charmbracelet/vhs/tree/main/examples)
- [`ttyd`](https://github.com/tsl0922/ttyd)
- [`ffmpeg`](https://ffmpeg.org)
- [THEMES.md](https://github.com/charmbracelet/vhs/blob/main/THEMES.md)
- [‚öôÔ∏è charmbracelet/vhs-action](https://github.com/charmbracelet/vhs-action)
- [üå≥ charmbracelet/tree-sitter-vhs](https://github.com/charmbracelet/tree-sitter-vhs)
- [Twitter](https://twitter.com/charmcli)
- [The Fediverse](https://mastodon.social/@charmcli)
- [Discord](https://charm.sh/chat)
- [MIT](https://github.com/charmbracelet/vhs/raw/main/LICENSE)
- [Charm](https://charm.sh)
- [<img src="https://img.shields.io/github/release/charmbracelet/vhs.svg" alt="Latest Release">](https://github.com/charmbracelet/vhs/releases)
- [<img src="https://godoc.org/github.com/golang/gddo?status.svg" alt="Go Docs">](https://pkg.go.dev/github.com/charmbracelet/vhs?tab=doc)
- [<img src="https://github.com/charmbracelet/vhs/workflows/build/badge.svg" alt="Build Status">](https://github.com/charmbracelet/vhs/actions)
- [<img alt="The Charm logo" width="400" src="https://stuff.charm.sh/charm-badge.jpg" />](https://charm.sh/)

--- THEMES.md ---
# Themes

* `3024 Day`
* `3024 Night`
* `Aardvark Blue`
* `Abernathy`
* `Adventure`
* `AdventureTime`
* `Afterglow`
* `Alabaster`
* `AlienBlood`
* `Andromeda`
* `Apple Classic`
* `arcoiris`
* `Argonaut`
* `Arthur`
* `AtelierSulphurpool`
* `Atom`
* `AtomOneLight`
* `Aurora`
* `ayu`
* `Ayu Mirage`
* `ayu_light`
* `Banana Blueberry`
* `Batman`
* `Belafonte Day`
* `Belafonte Night`
* `BirdsOfParadise`
* `Blazer`
* `Blue Matrix`
* `BlueBerryPie`
* `BlueDolphin`
* `BlulocoDark`
* `BlulocoLight`
* `Borland`
* `Breeze`
* `Bright Lights`
* `Broadcast`
* `Brogrammer`
* `Bubbles`
* `Builtin Dark`
* `Builtin Light`
* `Builtin Pastel Dark`
* `Builtin Solarized Dark`
* `Builtin Solarized Light`
* `Builtin Tango Dark`
* `Builtin Tango Light`
* `C64`
* `Calamity`
* `Catppuccin Frappe`
* `Catppuccin Latte`
* `Catppuccin Macchiato`
* `Catppuccin Mocha`
* `catppuccin-frappe`
* `catppuccin-latte`
* `catppuccin-macchiato`
* `catppuccin-mocha`
* `CGA`
* `Chalk`
* `Chalkboard`
* `ChallengerDeep`
* `Chester`
* `Ciapre`
* `CLRS`
* `Cobalt Neon`
* `Cobalt2`
* `coffee_theme`
* `Contrast Light`
* `coolnight`
* `CrayonPonyFish`
* `Crystal Violet`
* `Cyber Cube`
* `Cyberdyne`
* `cyberpunk`
* `CyberPunk2077`
* `Dark Pastel`
* `Dark+`
* `darkermatrix`
* `darkmatrix`
* `Darkside`
* `deep`
* `Desert`
* `DimmedMonokai`
* `Django`
* `DjangoRebornAgain`
* `DjangoSmooth`
* `Doom Peacock`
* `DoomOne`
* `DotGov`
* `Dracula`
* `Dracula+`
* `DraculaPlus`
* `duckbones`
* `Duotone Dark`
* `Earthsong`
* `Elemental`
* `Elementary`
* `ENCOM`
* `Espresso`
* `Espresso Libre`
* `Everblush`
* `Fahrenheit`
* `Fairyfloss`
* `Fideloper`
* `FirefoxDev`
* `Firewatch`
* `FishTank`
* `Flat`
* `Flatland`
* `Floraverse`
* `ForestBlue`
* `Framer`
* `FrontEndDelight`
* `FunForrest`
* `Galaxy`
* `Galizur`
* `Ganyu`
* `Github`
* `GitHub Dark`
* `Glacier`
* `Glorious`
* `Grape`
* `Grass`
* `Grey-green`
* `Gruvbox Light`
* `GruvboxDark`
* `GruvboxDarkHard`
* `Guezwhoz`
* `h4rithd`
* `h4rithd.com`
* `Hacktober`
* `Hardcore`
* `Harper`
* `HaX0R_BLUE`
* `HaX0R_GR33N`
* `HaX0R_R3D`
* `Highway`
* `Hipster Green`
* `Hivacruz`
* `Homebrew`
* `Hopscotch`
* `Hopscotch.256`
* `Horizon`
* `Hurtado`
* `Hybrid`
* `Hyper`
* `IC_Green_PPL`
* `IC_Orange_PPL`
* `iceberg-dark`
* `iceberg-light`
* `idea`
* `idleToes`
* `IR_Black`
* `iTerm2 Dark Background`
* `iTerm2 Default`
* `iTerm2 Light Background`
* `iTerm2 Pastel Dark Background`
* `iTerm2 Smoooooth`
* `iTerm2 Solarized Dark`
* `iTerm2 Solarized Light`
* `iTerm2 Tango Dark`
* `iTerm2 Tango Light`
* `Jackie Brown`
* `Japanesque`
* `Jellybeans`
* `JetBrains Darcula`
* `jubi`
* `Juicy Colors`
* `Kanagawa`
* `kanagawabones`
* `Kibble`
* `Kolorit`
* `Konsolas`
* `Lab Fox`
* `Laser`
* `Later This Evening`
* `Lavandula`
* `LiquidCarbon`
* `LiquidCarbonTransparent`
* `LiquidCarbonTransparentInverse`
* `lovelace`
* `Man Page`
* `Mariana`
* `Material`
* `MaterialDark`
* `MaterialDarker`
* `MaterialDesignColors`
* `MaterialOcean`
* `Mathias`
* `matrix`
* `Medallion`
* `midnight-in-mojave`
* `Mirage`
* `Misterioso`
* `Molokai`
* `MonaLisa`
* `Monokai Cmder`
* `Monokai Pro`
* `Monokai Pro (Filter Octagon)`
* `Monokai Pro (Filter Ristretto)`
* `Monokai Remastered`
* `Monokai Soda`
* `Monokai Vivid`
* `Moonlight II`
* `N0tch2k`
* `neobones_dark`
* `neobones_light`
* `Neon`
* `Neopolitan`
* `Neutron`
* `Night Owlish Light`
* `NightLion v1`
* `NightLion v2`
* `niji`
* `Nocturnal Winter`
* `nord`
* `nord-light`
* `Novel`
* `Obsidian`
* `Ocean`
* `Oceanic-Next`
* `OceanicMaterial`
* `Ollie`
* `OneDark`
* `OneHalfDark`
* `OneHalfLight`
* `OneStar`
* `Operator Mono Dark`
* `Overnight Slumber`
* `PaleNightHC`
* `Pandora`
* `Paraiso Dark`
* `PaulMillr`
* `PencilDark`
* `PencilLight`
* `Peppermint`
* `Piatto Light`
* `Pnevma`
* `Popping and Locking`
* `primary`
* `Primer`
* `Pro`
* `Pro Light`
* `Purple Rain`
* `purplepeter`
* `QB64 Super Dark Blue`
* `Rapture`
* `Raycast_Dark`
* `Raycast_Light`
* `rebecca`
* `Red Alert`
* `Red Planet`
* `Red Sands`
* `Relaxed`
* `Retro`
* `Retrowave`
* `Rippedcasts`
* `Rose Pine`
* `rose-pine`
* `rose-pine-dawn`
* `rose-pine-moon`
* `Rouge 2`
* `Royal`
* `Ryuuko`
* `Sakura`
* `Scarlet Protocol`
* `Seafoam Pastel`
* `SeaShells`
* `seoulbones_dark`
* `seoulbones_light`
* `Serendipity Midnight`
* `Serendipity Morning`
* `Serendipity Sunset`
* `Seti`
* `shades-of-purple`
* `Shaman`
* `Slate`
* `SleepyHollow`
* `Smyck`
* `Snazzy`
* `SoftServer`
* `Solarized Darcula`
* `Solarized Dark - Patched`
* `Solarized Dark Higher Contrast`
* `Sonoran Gothic`
* `Sonoran Sunrise`
* `Spacedust`
* `SpaceGray`
* `SpaceGray Eighties`
* `SpaceGray Eighties Dull`
* `Spiderman`
* `Spring`
* `Square`
* `Sublette`
* `Subliminal`
* `Sundried`
* `Symfonic`
* `synthwave`
* `synthwave-everything`
* `SynthwaveAlpha`
* `Tango Adapted`
* `Tango Half Adapted`
* `Teerb`
* `Terminal Basic`
* `Thayer Bright`
* `The Hulk`
* `Tinacious Design (Dark)`
* `Tinacious Design (Light)`
* `TokyoNight`
* `tokyonight`
* `tokyonight-day`
* `tokyonight-storm`
* `TokyoNightLight`
* `TokyoNightStorm`
* `Tomorrow`
* `Tomorrow Night`
* `Tomorrow Night Blue`
* `Tomorrow Night Bright`
* `Tomorrow Night Burns`
* `Tomorrow Night Eighties`
* `ToyChest`
* `Treehouse`
* `Twilight`
* `Ubuntu`
* `UltraDark`
* `UltraViolent`
* `UnderTheSea`
* `Unholy`
* `Unikitty`
* `Urple`
* `Vaughn`
* `VibrantInk`
* `vimbones`
* `Violet Dark`
* `Violet Light`
* `WarmNeon`
* `Wez`
* `Whimsy`
* `WildCherry`
* `wilmersdorf`
* `Wombat`
* `Wryan`
* `zenbones`
* `zenbones_dark`
* `zenbones_light`
* `Zenburn`
* `zenburned`
* `zenwritten_dark`
* `zenwritten_light`
* `Zeonica`


--- .github/ISSUE_TEMPLATE/bug_report.md ---
---
name: Bug report
about: Create a report to help us improve
title: ''
labels: ''
assignees: ''

---

**Describe the bug**
A clear and concise description of what the bug is.

**Setup**
Please complete the following information along with version numbers, if applicable.
 - OS [e.g. Ubuntu, macOS]
 - Shell [e.g. zsh, fish]
 - Terminal Emulator [e.g. kitty, iterm]
 - Terminal Multiplexer [e.g. tmux]

**To Reproduce**
Steps to reproduce the behavior:
1. Go to '...'
2. Click on '....'
3. Scroll down to '....'
4. See error

**Source Code**
Please include source code if needed to reproduce the behavior. 

**Expected behavior**
A clear and concise description of what you expected to happen.

**Screenshots**
Add screenshots to help explain your problem.

**Additional context**
Add any other context about the problem here.


--- .github/ISSUE_TEMPLATE/feature_request.md ---
---
name: Feature request
about: Suggest an idea for this project
title: ''
labels: enhancement
assignees: ''

---

**Is your feature request related to a problem? Please describe.**
A clear and concise description of what the problem is. Ex. I'm always frustrated when [...]

**Describe the solution you'd like**
A clear and concise description of what you want to happen.

**Describe alternatives you've considered**
A clear and concise description of any alternative solutions or features you've considered.

**Additional context**
Add any other context or screenshots about the feature request here.
